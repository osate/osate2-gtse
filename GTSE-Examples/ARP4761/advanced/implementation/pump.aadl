package impl::pump


public

with impl::common;
with ARP4761;
with EMV2;

	with SEI;

system pump
features
	pressure_output : provides bus access impl::common::pressure.i;
annex EMV2 {**                                                                                                                                                                  
	use types WBSImplementationErrorLib;
	use behavior WBSImplementationErrorLib::Twostate;
	
	error propagations
	--  The bindings information are here the
		bindings 		: out propagation {AnyImplementationError};
		pressure_output : out propagation {NoService};
	flows
		nopressure 		: error source pressure_output{NoService};
		nopressurefunc 	: error source bindings{AnyImplementationError};
	end propagations;
	
	component error behavior
	events
		HydraulicError : error event;
	transitions
		t1 : Operational -[ HydraulicError]-> Failed;
	propagations
		p1     : Failed 		-[]-> pressure_output{Noservice};
		p2     : Failed 		-[]-> bindings{AnyImplementationError}; 
		normal : Operational 	-[]-> pressure_output{NoError};
	end component;
	
	properties
		EMV2::Severity => ARP4761::Major applies to HydraulicError;
		EMV2::Likelihood => ARP4761::Probable applies to HydraulicError;
		EMV2::Hazards => 
			([	crossreference => "ARP4761 page 275 figure L9";
				failure => "Hydraulic Error on a pump";
				phases => ("TBD");
				description => "Loss of one pump";
				comment => "Major hazard if both pumps are lost";
			])
			applies to HydraulicError;
		-- EMV2::OccurrenceDistribution => [ ProbabilityValue => 1.20e-4 ; Distribution => Fixed;] applies to HydraulicError, pressure_output.NoService;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 2.5e-5 ; Distribution => Poisson;] applies to HydraulicError, pressure_output.NoService;**};
end pump;

-- XXX
-- TODO FIXME TODO
-- Move to ../functional/hydraulic.aadl
-- FIXME TODO FIXME
-- XXX
system implementation pump.generic
end pump.generic;

system implementation pump.large extends pump.generic
	properties
		SEI::Price => 500.00;
		SEI::NetWeight => 3.7 kg;
end pump.large;

system implementation pump.small extends pump.generic
	properties
		SEI::Price => 750.00;
		SEI::NetWeight => 1.32 kg;
end pump.small;


end impl::pump;
