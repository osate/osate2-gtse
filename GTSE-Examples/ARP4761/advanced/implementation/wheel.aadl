package impl::wheel

public

with impl::common, SEI, DemoProperties;

system wheel
end wheel;

system implementation wheel.i
	subcomponents
		tire : device tire;
		brake : device brake;
end wheel.i;

device tire
	
end tire;

device implementation tire.hi_quality
	properties
		-- Built-in properties supporting cost and weight analyses
		SEI::Price => 1000.0;
		SEI::NetWeight => 3.5 kg;
		-- Custom property supporting domain-specific analysis,
		--   potentially derived from other analysis / modeling tools
		DemoProperties::BrakingPower => 10.0;
end tire.hi_quality;

device implementation tire.lo_quality
	properties
		SEI::Price => 300.0;
		SEI::NetWeight => 2.5 kg;
		DemoProperties::BrakingPower => 5.0;
end tire.lo_quality;

device brake
	
end brake;

device implementation brake.hi_quality
	properties
		SEI::Price => 750.0;
		SEI::NetWeight => 2.5 kg;
		DemoProperties::BrakingPower => 10.0;
end brake.hi_quality;

device implementation brake.lo_quality
	properties
		SEI::Price => 350.0;
		SEI::NetWeight => 3.5 kg;
		DemoProperties::BrakingPower => 5.0;
end brake.lo_quality;

--
--  This is an example of one wheel with one
--  input. We do not represent the two channels,
--  we rather abstract the two channels into a
--  single one.
--
	system wheel_one_input extends wheel
features
	input : requires bus access impl::common::pressure.i;
annex EMV2 {**                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                            
	use types WBSImplementationErrorLib;
	use behavior WBSImplementationErrorLib::Twostate;
	
	error propagations
		input : in propagation{NoService};
	flows
		ns1 : error sink input{NoService};
	end propagations;
	
	component error behavior
	transitions
		t1 : Operational -[input{NoService}]-> Failed;
	end component;**};
end wheel_one_input;

system implementation wheel_one_input.i extends wheel.i
	properties
		SEI::Price => 350.00;
		SEI::NetWeight => 2.0 kg;
end wheel_one_input.i;


--
--  This is an example of a wheel with two
--  input, one from each pump. The other model
--  abstracts the two pumps in a single bus.
--  This model explicitly represents the two channels.
--

system wheel_two_inputs extends wheel_one_input
features
	green_input : requires bus access impl::common::pressure.i;
	blue_input : requires bus access impl::common::pressure.i;
annex EMV2 {**                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                         
	use types WBSImplementationErrorLib;
	use behavior WBSImplementationErrorLib::Twostate;
	error propagations
		green_input : in propagation{NoService};
		blue_input : in propagation{NoService};
	flows
		ns1 : error sink green_input{NoService};
		ns2 : error sink blue_input{NoService};
	end propagations;
	
	component error behavior
	transitions
		t1 : Operational -[ green_input{NoService}]-> Failed;
		t2 : Operational -[ blue_input{NoService}]-> Failed;
	end component;**};
end wheel_two_inputs;

system implementation wheel_two_inputs.i extends wheel.i
	properties
		SEI::Price => 500.0;
		SEI::NetWeight => 2.5 kg;
end wheel_two_inputs.i;



end impl::wheel;

